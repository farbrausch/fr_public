/*+**************************************************************************/
/***                                                                      ***/
/***   This file is distributed under a BSD license.                      ***/
/***   See LICENSE.txt for details.                                       ***/
/***                                                                      ***/
/**************************************************************************+*/

level11;

asc
{
  cbuffer CSTestPara : register (c0) : slot cs 0
  {
    float4 tx;
    float4 ty;
    float4 ofs;
    float4 potpos[4];
  };
}

cs CSTest
{
  asc cs_5_0
  {
    static const uint DIM = 13;
    static const uint DIM1 = DIM+1;
    static const uint DIM2 = DIM+2;
    static const uint DIM3 = DIM+3;
    
  
    use CSTestPara;
    
    RWByteAddressBuffer vb : register(u0);
    RWByteAddressBuffer ib : register(u1);
    RWStructuredBuffer<uint> vc : register(u2);
    RWStructuredBuffer<uint> ic : register(u3);
    
    Buffer<uint> mcmap : register(t0);
    StructuredBuffer<uint4> nodes : register(t1);
    
    groupshared float pot[4*DIM3*DIM3];
    groupshared int index[2*DIM1*DIM1*3];

    float func(float3 c)
    {
      c *= potpos[0].x;
      float x = c.x;
      float y = c.y;
      float z = c.z;

      // ===

      const int max = 5;
      int i = 0;
      float ab = 0;
      float ib = 0;
      do
      {
        float r2 = x*x+y*y;
        float r4 = r2*r2;
        float r6 = r2*r4;
        float r8 = r4*r4;

        float x2 = x*x;
        float x4 = x2*x2;
        float x6 = x2*x4;
        float x8 = x4*x4;

        float y2 = y*y;
        float y4 = y2*y2;
        float y6 = y2*y4;
        float y8 = y4*y4;

        float z2 = z*z;
        float z4 = z2*z2;
        float z6 = z2*z4;
        float z8 = z4*z4;

        float a = 1 + (z8 - 28*z6*r2 + 70*z4*r4 - 28*z2*r6) / (r8);

        float xx = a * (x8 - 28*x6*y2 + 70*x4*y4 - 28*x2*y6 + y8);
        float yy = (8*a*x*y) * (x6 - 7*x4*y2 + 7*x2*y4 - y6);
        float zz = (8*z*sqrt(r2)) * (z2-r2) * (z4 - 6*z2*r2 + r4);

        x = c.x+xx;
        y = c.y+yy;
        z = c.z+zz;

        bool run = ab<2;
        
        ab = run ? x*x+y*y+z*z : ab;
        ib = run ? ib+1 : ib;
        i++;
      }
      while(i<max && ab<2);
      return (i+2/ab)-4.5;
    }

    [numthreads(DIM3,DIM3,1)]
    void main
    (
      uint3 tid : SV_GroupThreadID,
      uint3 gid_ : SV_GroupID
    )
    {
      uint tidz; 
      uint4 gpos = nodes[gid_.x];
      float scale = 1.0/gpos.w;
      

      for(tidz=0;tidz<DIM3+1;tidz++)
      {
        
        // calc isovalues
        
        if(tidz<DIM3)
        {
          GroupMemoryBarrierWithGroupSync();
          tid.z = tidz;
          uint th = (tid.z&3)*DIM3*DIM3 + tid.y*DIM3 + tid.x;
          float3 p = (float3(tid+gpos.xyz*DIM-1)*scale+ofs.xyz)*ofs.w;
          pot[th] = func(p);
        }
      
        // emit vertices
  
        if(tidz>=3)
        {  
          GroupMemoryBarrierWithGroupSync();
          tid.z = tidz-3;
          
          if(tid.x<DIM1 && tid.y<DIM1)
          {
            float px,py,pz,p0;
            p0 = pot[((tid.z+1)&3)*DIM3*DIM3 + (tid.y+1)*DIM3 + (tid.x+1)];
            px = pot[((tid.z+1)&3)*DIM3*DIM3 + (tid.y+1)*DIM3 + (tid.x+2)];
            py = pot[((tid.z+1)&3)*DIM3*DIM3 + (tid.y+2)*DIM3 + (tid.x+1)];
            pz = pot[((tid.z+2)&3)*DIM3*DIM3 + (tid.y+1)*DIM3 + (tid.x+1)];
            bool4 b = float4(px,py,pz,p0)<=0;
            if(b.w!=b.x || b.w!=b.y || b.w!=b.z)
            {
              uint indexth = (tid.z&1)*DIM1*DIM1*3 + tid.y*DIM1*3 + tid.x*3;
              float3 pos0 = (float3(tid+gpos.xyz*DIM)*scale+ofs.xyz)*ofs.w;
              uint n;
              
              float3 n0;
              n0.x = pot[((tid.z+1)&3)*DIM3*DIM3 + (tid.y+1)*DIM3 + (tid.x+2)] - pot[((tid.z+1)&3)*DIM3*DIM3 + (tid.y+1)*DIM3 + (tid.x+0)];
              n0.y = pot[((tid.z+1)&3)*DIM3*DIM3 + (tid.y+2)*DIM3 + (tid.x+1)] - pot[((tid.z+1)&3)*DIM3*DIM3 + (tid.y+0)*DIM3 + (tid.x+1)];
              n0.z = pot[((tid.z+2)&3)*DIM3*DIM3 + (tid.y+1)*DIM3 + (tid.x+1)] - pot[((tid.z+0)&3)*DIM3*DIM3 + (tid.y+1)*DIM3 + (tid.x+1)];
              n0 = normalize(-n0);

              n = 0;
              if(b.w!=b.x)
              {
                float3 nx;
                nx.x = pot[((tid.z+1)&3)*DIM3*DIM3 + (tid.y+1)*DIM3 + (tid.x+2+1)] - pot[((tid.z+1)&3)*DIM3*DIM3 + (tid.y+1)*DIM3 + (tid.x+0+1)];
                nx.y = pot[((tid.z+1)&3)*DIM3*DIM3 + (tid.y+2)*DIM3 + (tid.x+1+1)] - pot[((tid.z+1)&3)*DIM3*DIM3 + (tid.y+0)*DIM3 + (tid.x+1+1)];
                nx.z = pot[((tid.z+2)&3)*DIM3*DIM3 + (tid.y+1)*DIM3 + (tid.x+1+1)] - pot[((tid.z+0)&3)*DIM3*DIM3 + (tid.y+1)*DIM3 + (tid.x+1+1)];
                nx = normalize(-nx);

                float f = p0/(p0-px);
                float3 norm = normalize(n0.xyz*f + nx.xyz*(1-f));
                float3 pos = pos0+float3(f*scale*ofs.w,0,0);

                n = vc.IncrementCounter();
                uint vp = n*24;

                vb.Store3(vp+ 0,asuint(pos));
                vb.Store3(vp+12,asuint(norm));
              }
              index[indexth+0] = n;

              n = 0;
              if(b.w!=b.y)
              {
                float3 ny;
                ny.x = pot[((tid.z+1)&3)*DIM3*DIM3 + (tid.y+1+1)*DIM3 + (tid.x+2)] - pot[((tid.z+1)&3)*DIM3*DIM3 + (tid.y+1+1)*DIM3 + (tid.x+0)];
                ny.y = pot[((tid.z+1)&3)*DIM3*DIM3 + (tid.y+2+1)*DIM3 + (tid.x+1)] - pot[((tid.z+1)&3)*DIM3*DIM3 + (tid.y+0+1)*DIM3 + (tid.x+1)];
                ny.z = pot[((tid.z+2)&3)*DIM3*DIM3 + (tid.y+1+1)*DIM3 + (tid.x+1)] - pot[((tid.z+0)&3)*DIM3*DIM3 + (tid.y+1+1)*DIM3 + (tid.x+1)];
                ny = normalize(-ny);

                float f = p0/(p0-py);
                float3 norm = normalize(n0.xyz*f + ny.xyz*(1-f));
                float3 pos = pos0+float3(0,f*scale*ofs.w,0);

                n = vc.IncrementCounter();
                uint vp = n*24;

                vb.Store3(vp+ 0,asuint(pos));
                vb.Store3(vp+12,asuint(norm));
              }
              index[indexth+1] = n;

              n = 0;
              if(b.w!=b.z)
              {
                float3 nz;
                nz.x = pot[((tid.z+1+1)&3)*DIM3*DIM3 + (tid.y+1)*DIM3 + (tid.x+2)] - pot[((tid.z+1+1)&3)*DIM3*DIM3 + (tid.y+1)*DIM3 + (tid.x+0)];
                nz.y = pot[((tid.z+1+1)&3)*DIM3*DIM3 + (tid.y+2)*DIM3 + (tid.x+1)] - pot[((tid.z+1+1)&3)*DIM3*DIM3 + (tid.y+0)*DIM3 + (tid.x+1)];
                nz.z = pot[((tid.z+2+1)&3)*DIM3*DIM3 + (tid.y+1)*DIM3 + (tid.x+1)] - pot[((tid.z+0+1)&3)*DIM3*DIM3 + (tid.y+1)*DIM3 + (tid.x+1)];
                nz = normalize(-nz);

                float f = p0/(p0-pz);
                float3 norm = normalize(n0.xyz*f + nz.xyz*(1-f));
                float3 pos = pos0+float3(0,0,f*scale*ofs.w);

                n = vc.IncrementCounter();
                uint vp = n*24;

                vb.Store3(vp+ 0,asuint(pos));
                vb.Store3(vp+12,asuint(norm));
              }
              index[indexth+2] = n;
            }
          }
        }
  
        // emit indices
          
        if(tidz>=4)
        {
          GroupMemoryBarrierWithGroupSync();
          tid.z = tidz-4;
          
          uint code0 = 0x000;
          if(tid.z & 1)
            code0 = 0x100*5;
            
          if(tid.x<DIM && tid.y<DIM)
          {
            float4 pot0,pot1;

            pot0.x = pot[((tid.z+1)&3)*DIM3*DIM3 + (tid.y+1)*DIM3 + (tid.x+1)];
            pot0.y = pot[((tid.z+1)&3)*DIM3*DIM3 + (tid.y+1)*DIM3 + (tid.x+2)];
            pot0.z = pot[((tid.z+1)&3)*DIM3*DIM3 + (tid.y+2)*DIM3 + (tid.x+2)];
            pot0.w = pot[((tid.z+1)&3)*DIM3*DIM3 + (tid.y+2)*DIM3 + (tid.x+1)];
            pot1.x = pot[((tid.z+2)&3)*DIM3*DIM3 + (tid.y+1)*DIM3 + (tid.x+1)];
            pot1.y = pot[((tid.z+2)&3)*DIM3*DIM3 + (tid.y+1)*DIM3 + (tid.x+2)];
            pot1.z = pot[((tid.z+2)&3)*DIM3*DIM3 + (tid.y+2)*DIM3 + (tid.x+2)];
            pot1.w = pot[((tid.z+2)&3)*DIM3*DIM3 + (tid.y+2)*DIM3 + (tid.x+1)];
            
            uint4 code4;
            code4  = (pot0<=0) ? uint4(0x01*5,0x02*5,0x04*5,0x08*5) : uint4(0,0,0,0);
            code4 += (pot1<=0) ? uint4(0x10*5,0x20*5,0x40*5,0x80*5) : uint4(0,0,0,0);
            code4.xy = code4.yx+code4.zw;
            uint code = code4.x+code4.y+code0;

            uint indexth = tid.y*DIM1*3 + tid.x*3;
            for(int i=0;i<5 && mcmap[code]!=0xffffffff;i++)
            {
              uint offsets = mcmap[code];
              code++;
              
              uint n = ic.IncrementCounter();
              uint3 ind;
              ind.x = index[indexth+((offsets>>0 )&0x3ff)];
              ind.y = index[indexth+((offsets>>10)&0x3ff)];
              ind.z = index[indexth+((offsets>>20)&0x3ff)];
              ib.Store3(n*3*4,ind);
            }
          }
          GroupMemoryBarrierWithGroupSync();
        }
      }
    }
  }
}

cs WriteCount
{
  asc cs_5_0
  {
    RWStructuredBuffer<uint> cnt : register(u0);
    RWBuffer<uint> ind : register(u1);
    [numthreads(1,1,1)]
    void main()
    {
      ind[0] = cnt.IncrementCounter()*3;
      ind[1] = 1;
      ind[2] = 0;
      ind[3] = 0;
      ind[4] = 0;
    }
  }
}

/**************************************************************************+*/

asc
{
  cbuffer MaterialFlatPara : register(c0)
  {
    row_major float4x4 mvp;
    float4 ldir;
  };
}

material MaterialFlat
{
  // the vertex shader

  vs
  {    
    asc vs_5_0                 // hlsl code
    {
      use MaterialFlatPara;

      void main
      (
        in float3 in_pos : POSITION,            // input, from vertex array
        in float3 in_norm : NORMAL,
        out float2 out_uv : TEXCOORD0,          // output, to pixel shader. 
        out float4 out_col : COLOR,
        out float4 out_pos : POSITION,          // position last for DX11
      ) 
      {
        out_uv = in_pos.xy;
        float l = saturate(in_norm ° ldir.xyz); // this is a dot product!
        out_col = float4(l,l,l,1);
        out_pos = mul(float4(in_pos,1),mvp);
        
      }
    }
  }

  // the pixel shader

  ps
  {

    asc ps_5_0
    {
      SamplerState sam0 : register(s0);
      Texture2D tex0 : register(t0);
      void main
      (
        in float2 uv : TEXCOORD0,
        in float4 col : COLOR0,
        out float4 result : COLOR0
      )
      {
        result = tex0.Sample(sam0,uv)*col;
      }
    }
  }
};

/**************************************************************************+*/
